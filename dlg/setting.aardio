import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="设置";right=984;bottom=589;bgcolor=16777215)
winform.add(
btnOpenIcon={cls="plus";text='\uF115';left=815;top=424;right=844;bottom=447;color=2960685;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=17};
btnSave={cls="plus";text='\uF0C7   更新设置';left=636;top=526;right=838;bottom=562;bgcolor=11580047;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=4};
btnSelectFont={cls="plus";text='\uF031';left=571;top=469;right=600;bottom=492;color=2960685;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=21};
chkDefaultModeEn={cls="plus";text="默认英文模式（ 可短按 Shift 键切换，WIN10 20H1以上版本可自定义快捷键 )";left=156;top=144;right=769;bottom=175;align="left";dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=24};z=2};
chkEnableSystemRun={cls="plus";text="允许开机启动 wubiLex （ 启动时不再需要确认管理权限，建议开启 ）";left=156;top=309;right=732;bottom=340;align="left";dl=1;dr=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=24};z=12};
chkEnableWubiUdp={cls="plus";text="允许自定义短语";left=156;top=256;right=412;bottom=287;align="left";dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=24};z=6};
chkHalfWidthInputModeByDefault={cls="plus";text="英文标点默认使用半角输入模式（ 可按 Shift + 空格键 切换 ）";left=156;top=181;right=741;bottom=212;align="left";dl=1;dr=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=24};z=14};
chkUseEnglishPunctuationsInChineseInputMode={cls="plus";text="中文输入时使用英文标点（ WIN10 20H1以上版本支持 Ctrl + . 切换 ）";left=156;top=219;right=730;bottom=250;align="left";dl=1;dr=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=24};z=15};
chkWubiAutoFinalize={cls="plus";text="四码唯一时自动上屏（ 拼音混输时不建议开启 ）";left=156;top=106;right=596;bottom=137;align="left";dl=1;dr=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=24};z=11};
editMaxCandidates={cls="plus";left=768;top=460;right=810;bottom=489;align="left";border={bottom=1;color=-16744448};dr=1;font=LOGFONT(h=-16);notify=1;tabstop=1;textPadding={top=6;bottom=1};z=23};
editWubiDescription={cls="plus";left=284;top=365;right=565;bottom=398;align="left";border={bottom=1;color=-16744448};dl=1;dr=1;editable=1;font=LOGFONT(h=-16);tabstop=1;textPadding={top=6;bottom=1};z=10};
editWubiFont={cls="plus";left=284;top=456;right=573;bottom=489;border={bottom=1;color=-16744448};dl=1;dr=1;editable=1;font=LOGFONT(h=-16);notify=1;tabstop=1;textPadding={top=6;bottom=1};z=8};
editWubiIcon={cls="plus";left=284;top=411;right=810;bottom=444;align="left";border={bottom=1;color=-16744448};dl=1;dr=1;editable=1;font=LOGFONT(h=-16);tabstop=1;textPadding={top=6;bottom=1};z=7};
plus={cls="plus";left=160;top=346;right=684;bottom=356;border={bottom=1;color=-4144960};dl=1;dr=1;z=24};
plus2={cls="plus";left=160;top=86;right=684;bottom=96;border={bottom=1;color=-4144960};dl=1;dr=1;z=13};
plus3={cls="plus";left=160;top=286;right=684;bottom=296;border={bottom=1;color=-4144960};dl=1;dr=1;z=16};
plusOpenHotkeys={cls="plus";text='\uF11C   打开系统输入法热键设置';left=361;top=526;right=601;bottom=562;bgcolor=14935259;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=9};
plusOpenMsSetting={cls="plus";text='\uF17A   打开系统五笔设置';left=149;top=526;right=350;bottom=562;bgcolor=14935259;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=5};
radioPinyinMixDisable={cls="plus";text="纯五笔输入";left=156;top=25;right=318;bottom=56;align="left";dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF111 ';notify=1;textPadding={left=24};z=1};
radioPinyinMixEnable={cls="plus";text="五笔拼音混输";left=156;top=58;right=318;bottom=89;align="left";dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='FontAwesome')};iconText='\uF111 ';notify=1;textPadding={left=24};z=3};
static={cls="static";text="输入法显示名称:";left=135;top=379;right=275;bottom=406;align="right";dl=1;font=LOGFONT(h=-16;name='FontAwesome');transparent=1;z=18};
static2={cls="static";text="输入法显示图标:";left=135;top=425;right=275;bottom=452;align="right";dl=1;font=LOGFONT(h=-16;name='FontAwesome');transparent=1;z=19};
static3={cls="static";text="候选词字体:";left=135;top=471;right=275;bottom=498;align="right";dl=1;font=LOGFONT(h=-16;name='FontAwesome');transparent=1;z=20};
static4={cls="static";text="候选词每页显示数:";left=617;top=471;right=758;bottom=498;align="right";dr=1;font=LOGFONT(h=-16;name='FontAwesome');transparent=1;z=22}
)
/*}}*/

import style;
winform.radioPinyinMixEnable.skin(style.radio)
winform.radioPinyinMixDisable.skin(style.radio)
winform.chkDefaultModeEn.skin(style.checkBox)  
winform.chkEnableWubiUdp.skin(style.checkBox)
winform.chkEnableSystemRun.skin(style.checkBox)
winform.chkWubiAutoFinalize.skin(style.checkBox)
winform.chkHalfWidthInputModeByDefault.skin(style.checkBox)
winform.chkUseEnglishPunctuationsInChineseInputMode.skin(style.checkBox)
winform.btnSave.skin(style.primaryButton)
winform.btnOpenIcon.skin( style.plainButton )
winform.plusOpenMsSetting.skin(style.button)
winform.plusOpenHotkeys.skin(style.button)

import win.reg;
var regSettings = win.reg("HKEY_CURRENT_USER\Software\Microsoft\InputMethod\Settings\CHS");
var setting = regSettings.queryValueTable({
	["Default Mode"] = 1;
	["Enable Wubi EUDP"] = 1;
	["WubiAutoFinalizeForMixEnable"] = 1;
	["Wubi Auto-finalize Enable"] = 1;
	["HalfWidthInputModeByDefault"] = 1;
	["UseEnglishPunctuationsInChineseInputMode"] = 0;
})
regSettings.close();

winform.radioPinyinMixEnable.checked = setting["PinyinMixEnable"];
winform.radioPinyinMixDisable.checked = !setting["PinyinMixEnable"];
winform.chkDefaultModeEn.checked = setting["Default Mode"];
winform.chkEnableWubiUdp.checked = setting["Enable Wubi EUDP"];
winform.chkUseEnglishPunctuationsInChineseInputMode.checked = setting["UseEnglishPunctuationsInChineseInputMode"];
winform.chkHalfWidthInputModeByDefault.checked = setting["HalfWidthInputModeByDefault"];

if(winform.radioPinyinMixEnable.checked){
	winform.chkWubiAutoFinalize.checked = setting["WubiAutoFinalizeForMixEnable"]
}
else {
	winform.chkWubiAutoFinalize.checked = setting["Wubi Auto-finalize Enable"]
}

var reg = win.reg("HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\CTF\TIP\{6a498709-e00b-4c45-a018-8f9e4081ae40}\LanguageProfile\0x00000804\{82590C13-F4DD-44f4-BA1D-8667246FDF8E}");	
var wubiDisplayDescription = reg.queryValue("Display Description")
var wubiIconFile = reg.queryValue("IconFile")
var wubiIconIndex = reg.queryValue("IconIndex")
reg.close();

if(wubiDisplayDescription!="@%SystemRoot%\SYSTEM32\input.dll,-5302"){
	winform.editWubiDescription.text = wubiDisplayDescription;	
}

if(wubiIconFile!="%SystemRoot%\system32\InputMethod\Shared\ResourceDll.dll"){
	winform.editWubiIcon.text = wubiIconFile +","+wubiIconIndex;
}

winform.editWubiDescription.setCueBannerText("默认名称");
winform.editWubiIcon.setCueBannerText("默认图标");
winform.editWubiFont.setCueBannerText("默认字体");

winform.editMaxCandidates.createEditBox( 
	cls = "edit";
	num = 1;  
	limit = 2;
);

var reg = win.reg("HKEY_CURRENT_USER\Software\Microsoft\InputMethod\CandidateWindow\CHS\2");
winform.editWubiFont.text = reg.queryValue("FontStyle");
winform.editMaxCandidates.text = reg.queryValue("MaxCandidates") : "7";
if(!reg.queryValue("EnableFixedCandidateCountMode")){
	winform.editMaxCandidates.text = 0;
}
reg.close();

import app;
import config;
import win.reg;
import tsfUtil;
import key.ime;
winform.btnSave.oncommand = function(id,event){
    var maxCandidates = winform.editMaxCandidates.text + 0;
    if(maxCandidates){
  		if(maxCandidates<3){
			maxCandidates=3;
			winform.editMaxCandidates.editBox.showWarningTip("wubiLex","候选词数目不能小于3")
			return;
		}
		if(maxCandidates>9){
			maxCandidates=9;
			winform.editMaxCandidates.editBox.showWarningTip("wubiLex","候选词数目不能大于9")
			return;
		}  	
    } 
	
	winform.btnSave.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	var reg = win.reg("HKEY_CURRENT_USER\Software\Microsoft\InputMethod\CandidateWindow\CHS\2");
	var fontStyle = string.trim(winform.editWubiFont.text);
	if(!#fontStyle){
		fontStyle = "13.50pt;Regular;;Microsoft YaHei UI";
	}
	reg.setSzValue("FontStyle",fontStyle) 
	if(maxCandidates){
		reg.setDwValue("MaxCandidates",maxCandidates);
		reg.setDwValue("EnableFixedCandidateCountMode",1);
	}
	else {
		reg.setDwValue("MaxCandidates",0);
		reg.setDwValue("EnableFixedCandidateCountMode",0);
	}
	
	
	reg.close();
	
	var regSettings = win.reg("HKEY_CURRENT_USER\Software\Microsoft\InputMethod\Settings\CHS");
	regSettings.setDwValue("Floating Icon Time Key",0);
	regSettings.setDwValue("PinyinMixEnable",winform.radioPinyinMixEnable.checked?1:0);
	regSettings.setDwValue("Default Mode",winform.chkDefaultModeEn.checked?1:0);
	regSettings.setDwValue("Enable Wubi EUDP",winform.chkEnableWubiUdp.checked?1:0);
	
	regSettings.setDwValue("HalfWidthInputModeByDefault",winform.chkHalfWidthInputModeByDefault.checked?1:0);
	regSettings.setDwValue("UseEnglishPunctuationsInChineseInputMode",winform.chkUseEnglishPunctuationsInChineseInputMode.checked?1:0)
	
	if(winform.radioPinyinMixEnable.checked){
		regSettings.setDwValue("WubiAutoFinalizeForMixEnable",winform.chkWubiAutoFinalize.checked?1:0);
	}
	else {
		regSettings.setDwValue("Wubi Auto-finalize Enable",winform.chkWubiAutoFinalize.checked?1:0);
	}
	regSettings.close();
	
	var reg = win.reg("HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\CTF\TIP\{6a498709-e00b-4c45-a018-8f9e4081ae40}\LanguageProfile\0x00000804\{82590C13-F4DD-44f4-BA1D-8667246FDF8E}");	
	if(#winform.editWubiDescription.text){
		reg.setSzValue("Display Description",winform.editWubiDescription.text); 
	}
	else {
		reg.setSzValue("Display Description","@%SystemRoot%\SYSTEM32\input.dll,-5302"); 
	}
	
	var iconPath = string.trim(winform.editWubiIcon.text);
	if(#iconPath){
		var iconFile,iconIndex = string.match(iconPath,"(.+),(\d+)")
		if(!a){
			iconFile = iconPath;
			iconIndex = 0;
		}
		reg.setSzValue("IconFile",iconFile) 
	 	reg.setDwValue("IconIndex",iconIndex+0) 
		
	}
	else {
		reg.setSzValue("IconFile","%SystemRoot%\system32\InputMethod\Shared\ResourceDll.dll") 
		reg.setDwValue("IconIndex",1) 
	}
	reg.close();
	

	tsfUtil.reset()
	key.ime.changeRequest(0x4090409); 
	
	if(!_STUDIO_INVOKED){
		config.settings.systemStartup = winform.chkEnableSystemRun.checked;
		if(winform.chkEnableSystemRun.checked){
			app.systemStartup("wubiLex( 五笔助手 ) 启动任务","用于微软五笔的辅助工具。",io._exepath,"/tray")
		}
		else {
			app.systemStartup("wubiLex( 五笔助手 ) 启动任务",null);
		}	
	}

	winform.msgOk('更新成功，如果不能输入或设置未生效，\n请按 Alt + Shift 来回切换一下输入法即可');
	winform.btnSave.disabledText = null;
}
winform.chkEnableSystemRun.checked = config.settings.systemStartup;

import process;
winform.plusOpenMsSetting.oncommand = function(id,event){
	process.execute("ms-settings:regionlanguage-chsime-wubi");
}

import process.rundll;
winform.plusOpenHotkeys.oncommand = function(id,event){
	process.rundll(,"Control_RunDLL input.dll,,{C07337D3-DB2C-4D0B-9A93-B722A6C106E2}{HOTKEYS}")
}

import fsys.dlg;
winform.btnOpenIcon.oncommand = function(id,event){
	var path = fsys.dlg.open("*.ico|*.ico|*.exe:*.dll|*.exe:*.dll||")
	if(path){
		if(!string.endWith(path,".ico",true)){
			path = path + ",1";
		}
		
		winform.editWubiIcon.text = path;
	}
}

import win.dlg.font;
winform.btnSelectFont.skin(style.plainButton)
winform.btnSelectFont.oncommand = function(id,event){
	var dlg = win.dlg.font(winform)
	var strFont = winform.editWubiFont.text;
	if(!#strFont){
		strFont ="13.50pt;Regular;;Microsoft YaHei UI"
	}
	
	var fontSize,fontType,fontName = string.match(strFont,"([\d\.]+)pt\s*\;\s*(\w+)\s*\;.*\;\s*(.+)");
	if(fontSize&&fontType&&fontName){
		dlg.logFont = ::LOGFONT(name=fontName;point=fontSize)
	}
	
	var font = dlg.chooseFont()
	if(font){
		winform.editWubiFont.text = string.format("%dpt;Regular;;%s",font.getPointSize(),font.name )
	}
}

winform.editMaxCandidates.editBox.translateAccelerator = function(msg){ 
    if( msg.message != 0x100/*_WM_KEYDOWN*/) return;
    
    var vk = msg.wParam;
 	if( vk == 0x26/*_VK_UP*/ ){
		var maxCandidates = winform.editMaxCandidates.text+1;
		if(maxCandidates>9){
			maxCandidates = 9;
		}
		elseif(maxCandidates<3){
			maxCandidates = 3;
		}
		 
		winform.editMaxCandidates.text = maxCandidates;
		return true;
	}	
	elseif( vk == 0x28/*_VK_DOWN*/){
		var maxCandidates = winform.editMaxCandidates.text-1;
		if(maxCandidates>9){
			maxCandidates = 9;
		}
		elseif(maxCandidates<3){
			maxCandidates = 0;
		}
		 
		winform.editMaxCandidates.text = maxCandidates;
		return true;
	}
}

import win.ui.tooltip; 
var tooltipCtrl = win.ui.tooltip( winform );
tooltipCtrl.addTool(winform.editMaxCandidates.editBox,"可以按上下方向键增减数字,数字必须在3到9之间,0表示固定宽度" )
tooltipCtrl.addTool(winform.btnSelectFont,"点这里选择候选词显示字体" )
tooltipCtrl.addTool(winform.btnOpenIcon,"点这里选择输入法显示图标" )
tooltipCtrl.addTool(winform.editWubiFont.editBox,"清空则恢复默认字体")

winform.enableDpiScaling();
winform.show();
win.loopMessage();
return winform;